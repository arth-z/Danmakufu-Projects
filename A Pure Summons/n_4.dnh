#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Nonspell 4"]
#Text["More filler, I guess? [r] Man, I don't do much [r] with Junko's nons."]
#System["./JunkoSystem.dnh"]
#Background["./JunkoBackground.dnh"]

#include "./func_library.dnh"
#include "./img/ZUNShot_Const.txt"
#include "./JunkoFX.dnh"

let mainTimer = 0;

@Event 
{
  alternative(GetEventType())
    case(EV_REQUEST_LIFE){ //Sets boss's life
      SetScriptResult(4500);
    }
    case(EV_REQUEST_TIMER){ //Sets attack time
      SetScriptResult(60);
    }
    case(EV_REQUEST_SPELL_SCORE){ //Spellcard bonus
      SetScriptResult(1000000);
    }
}

@Initialize 
{ 
  objScene = GetEnemyBossSceneObjectID();
  objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objBoss);

  TLoadJunkoFX;
  TNotifyBattleEvents;

  ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY()/1.25, 60);

  TJunkoHitFX;
  TJunkoBurnAura;
  TJunkoCircleAura;
  TJunkoShadowAura;
  TJunkoCrownAura;
  TDistortionAura;
  
  MainTask;

  TJunkoFinalize;
}

let new_x = GetCenterX();
let new_y = GetCenterY()*0.6;

@MainLoop 
{
  ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
  ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
  
  let bossX = ObjMove_GetX(objBoss);
  let bossY = ObjMove_GetY(objBoss);

  // movement constants
  let move_interval = 360;
  let move_time = 60;

  // random movement control
  if (mainTimer % move_interval < move_time) {
    if (mainTimer % move_interval == 0) {
      new_x = GetCenterX() + rand(-50, 50);
      new_y = GetCenterY()*0.6 + rand(-10,10);
      ObjMove_SetDestAtFrame(objBoss, new_x, new_y, move_time);
    } else {
      MDrawJunkoMove(mainTimer % move_interval, move_interval, new_x, new_y, move_time);
    }
  } else {
    MDrawJunkoBlink(mainTimer);
    MJunkoBreathe(mainTimer);
  }

  if (mainTimer < 60) {ObjEnemy_SetDamageRate(objBoss, 25, 25)} else {ObjEnemy_SetDamageRate(objBoss, 100, 100)}

  mainTimer++;
  yield;
}

task collapse_circle(posX, posY, radius, offset, inc, proj, accel, max, direction) {
  
  let angle = offset;
  let angle_inc = inc;
  let rand_ang = rand(-60,60);

  loop(360/angle_inc) {
    if (ObjEnemy_GetInfo(objBoss,INFO_LIFE) > 0 && Obj_IsDeleted(objBoss)) {return;}
    angle+=angle_inc;
    let shot = CreateShotA2(posX+radius*cos(angle), posY+radius*sin(angle), 0, -angle+rand_ang, accel, max, proj, 10);
    ObjMove_SetAngularVelocity(shot, direction);
    ObjRender_SetBlendType(shot, BLEND_ADD_ARGB);
  }

}

task MainTask {

  wait(60);
  let loop_iters = 0;
  let laser_sfx = SoundSFX(GetCurrentScriptDirectory() ~ "./sfx/se_kira00.wav");
  let orbs = [];
  let fire_interval = 120;
  let floral_density = 6;
  let direction = 1;

  // circles spawn around junko, overflowing blemishes callback, except they "degenerate" or "collapse"
  // into walls/waves that converge on you. also they have angular velocity, to be mean.
  // a surprisingly difficult dodge, despite the simplicity
  while(ObjEnemy_GetInfo(objBoss,INFO_LIFE) > 0){
    let bossX = ObjMove_GetX(objBoss);
    let bossY = ObjMove_GetY(objBoss);

    if (loop_iters % fire_interval == 0) {
      let playerX = GetPlayerX();
      let playerY = GetPlayerY();
      TJunkoShockwave(150, 80, [255, 50, 50]);
      wait(15);
      TJunkoShockwave(150, 80, [100, 100, 255]);
      ascent(i in 0..4) {
        if (ObjEnemy_GetInfo(objBoss,INFO_LIFE) <= 0){return}
        collapse_circle(bossX, bossY, 75, rand(0, 360), 7.5, FIRE_RED, 0.015, 3, -0.5*direction);
        PlaySFX(laser_sfx);

        wait(30);

        if (ObjEnemy_GetInfo(objBoss,INFO_LIFE) <= 0){return}
        collapse_circle(bossX, bossY, 125, rand(0, 360), 12, FIRE_BLUE, 0.02, 4, 0.5*direction);
        PlaySFX(laser_sfx);

        wait(30);
      }
      direction*=-1;
    }  
    loop_iters++;
    yield;
  }
  
}