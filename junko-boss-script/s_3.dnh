#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Squirrel's Junko - Flower Spell 1"]
#Text["I hope you enjoy reading diagonal curvy lasers >:)"]
#System["./JunkoSystem.dnh"]
#Background["./JunkoBackground.dnh"]

#include "./func_library.dnh"
#include "./img/ZUNShot_Const.txt"
#include "./JunkoFX.dnh"

let mainTimer = 0;

@Event 
{
  alternative(GetEventType())
    case(EV_REQUEST_LIFE){ //Sets boss's life
        SetScriptResult(3000);
    }
    case(EV_REQUEST_TIMER){ //Sets attack time
        SetScriptResult(70);
    }
    case(EV_REQUEST_SPELL_SCORE){ //Spellcard bonus
        SetScriptResult(1000000);
    }
}

@Initialize 
{ 
  objScene = GetEnemyBossSceneObjectID();
  objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objBoss);
  TJunkoSpellStartFX("Pure Blossoming of Hatred", DISPLEASED, 255, 100, 100);

  ObjEnemyBossScene_Regist(objBoss);
  ObjEnemyBossScene_StartSpell(objScene);
  ObjEnemy_Regist(objBoss);

  TLoadJunkoFX;

  ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY()/1.25, 60);

  TJunkoHitFX;
  TJunkoBurnAura;
  TJunkoCircleAura;
  TJunkoShadowAura;
  TJunkoCrownAura;
  
  MainTask;

  TJunkoFinalize;
}

let new_x = GetCenterX();
let new_y = GetCenterY()*0.5;

@MainLoop 
{
  ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
  ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
  
  let bossX = ObjMove_GetX(objBoss);
  let bossY = ObjMove_GetY(objBoss);

  // movement constants
  let move_interval = 240;
  let move_time = 60;

  // random movement control
  if (mainTimer % move_interval < move_time) {
    if (mainTimer % move_interval == 0) {
      new_x = GetCenterX() + rand(-50, 50);
      new_y = GetCenterY()*0.5 + rand(-10,10);
      ObjMove_SetDestAtFrame(objBoss, new_x, new_y, move_time);
    } else {
      MDrawJunkoMove(mainTimer % move_interval, move_interval, new_x, new_y, move_time);
    }
  } else {
    MDrawJunkoBlink(mainTimer);
    MJunkoBreathe(mainTimer);
  }

  if (mainTimer < 200) {ObjEnemy_SetDamageRate(objBoss, 25, 25)} else {ObjEnemy_SetDamageRate(objBoss, 100, 100)}
  if (mainTimer == 60) {TJunkoChargeUp(200, 80, [255, 80, 80])}

  mainTimer++;
  yield;
}


function floral_laser_circle(posX, posY, offset, inc, speed, proj, angv, length, width) {
  let angle = offset;
  let angle_inc = inc;
  let direction = -1;
  let lasers = [];

  loop(360/angle_inc) {
    angle+=angle_inc;
    let objLaser = CreateCurveLaserA1(posX, posY, speed, angle,
        length, width, proj, 0);
    ObjCrLaser_SetTipDecrement(objLaser, 0.5);
    ObjMove_SetAngularVelocity(objLaser, angv*direction);
    direction*=-1;
    lasers = lasers ~ [objLaser];
  }

  return lasers;
}

task spin_circle(posX, posY, offset, inc, proj, accel, max, direction) {
  
  let angle = offset;
  let angle_inc = inc;

  loop(360/angle_inc) {
    angle+=angle_inc;
    let shot = CreateShotA2(posX, posY, 0, angle, accel, max, proj, 10);
    ObjMove_SetAngularVelocity(shot, direction*0.5);
    ObjShot_SetDeleteFrame(shot, 300);
  }

  angle += offset;

}

task MainTask {

  wait(200);
  let loop_iters = 0;
  let laser_sfx = SoundSFX(GetCurrentScriptDirectory() ~ "./sfx/se_lazer00.wav");
  let orb_sfx = SoundSFX(GetCurrentScriptDirectory() ~ "./sfx/se_option.wav");
  let lasers = [];
  let orbs = [];
  let fire_interval = 120;
  let bloom_interval = fire_interval*4;
  let angle = rand(0, 360);



  while(ObjEnemy_GetInfo(objBoss,INFO_LIFE) > 0){
    let bossX = ObjMove_GetX(objBoss);
    let bossY = ObjMove_GetY(objBoss);

    if(true) {
      if (loop_iters % fire_interval == fire_interval/2) {
        ascent(i in 0..length(orbs)) {

        let orbX = ObjMove_GetX(orbs[i]);
        let orbY = ObjMove_GetY(orbs[i]);

        if (!Obj_IsDeleted(orbs[i])) {
          lasers = lasers ~ floral_laser_circle(orbX, orbY, angle, 20, 5, 40, 7, 30, 20); 
          spin_circle(orbX, orbY, angle, 24, 235, 0.1, 3, power(-1, i));
          spin_circle(orbX, orbY, angle, 24, 235, 0.1, 3, -power(-1, i));
          ObjShot_FadeDelete(orbs[i]);
        }
        
      }
      PlaySFX(laser_sfx);
      orbs = [];
      } else if (loop_iters % fire_interval == 0) {
        let angleP = GetAngleToPlayer(objBoss);
        let orb = CreateShotA2(bossX, bossY, 0, 0, 0, 0, 399, 0);

        let predictX = GetPlayerX();
        let predictY = GetPlayerY();

        ObjMove_SetDestAtWeight(orb, predictX, predictY, 20, 10);
        TJunkoShockwave(200, 80, [255, 80, 255]);
        PlaySFX(orb_sfx);
        orbs = orbs ~ [orb];
        angle = rand(0, 360);
      }

      if (loop_iters % fire_interval  == 0) {
        ascent(i in 0..length(lasers)) {
          ObjMove_SetAngularVelocity(lasers[i], -power(-1, i)*1.25);
          ObjShot_SetDeleteFrame(lasers[i], 180)
        }
      lasers = [];
      }
    }

    loop_iters++;
    yield;
  }
  
}