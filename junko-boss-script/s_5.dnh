#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Squirrel's Junko - Spellcard \"Purple Star\""]
#Text["I promise this is the last time you'll see these assholes."]
#System["./JunkoSystem.dnh"]
#Background["./JunkoBackground.dnh"]

#include "./func_library.dnh"
#include "./img/ZUNShot_Const.txt"
#include "./JunkoFX.dnh"
#include "./HatefulStar.dnh"

let mainTimer = 0;

@Event 
{
  alternative(GetEventType())
    case(EV_REQUEST_LIFE){ //Sets boss's life
        SetScriptResult(50);
    }
    case(EV_REQUEST_TIMER){ //Sets attack time
        SetScriptResult(40);
    }
    case(EV_REQUEST_SPELL_SCORE){ //Spellcard bonus
        SetScriptResult(2000000);
    }
    case(EV_REQUEST_IS_DURABLE_SPELL) {
        SetScriptResult(true);
    }
}

@Initialize 
{
  objScene = GetEnemyBossSceneObjectID();
  objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);

  ObjEnemyBossScene_Regist(objBoss);
  ObjEnemyBossScene_StartSpell(objScene);
  ObjEnemy_Regist(objBoss);

  TLoadJunkoFX;

  ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY()*0.5, 60);

  TJunkoBurnAura;
  TJunkoCircleAura;
  TJunkoShadowAura;
  TJunkoCrownAura;
  
  TStarInitialise(GetCenterX(), GetCenterY(), 0, 0);
  TStarDraw(200, "purple");
  TStarHitFX;
  
  TJunkoSpellStartFX("Lunatic Star \"Dissolution\"", NEUTRAL, 255, 50, 255);

  MainTask;

  TJunkoFinalize;
  TStarFinalize;
}

let new_x = GetCenterX();
let new_y = GetCenterY();

@MainLoop 
{ 
  let bossX = ObjMove_GetX(objBoss);
  let bossY = ObjMove_GetY(objBoss);

  // movement constants
  let move_interval = 120;
  let move_time = 150;

  if (mainTimer < 16) {
    ObjEnemy_SetDamageRate(objBoss, 25, 25);
    MDrawJunkoMotionBegin(mainTimer);
  } else if (mainTimer < 44) {
    MDrawJunkoMotionContinue(mainTimer);
  } else if (mainTimer < 60) {
    MDrawJunkoMotionEnd(mainTimer);
  } else if (mainTimer < 200) {
    MDrawJunkoLaughContinue(mainTimer);
    MJunkoBreathe(mainTimer);
  } else {
    if (mainTimer == 205) {
      new_x = GetCenterX();
      new_y = GetCenterY()*-1.7;
      ObjMove_SetDestAtWeight(objBoss, new_x, new_y, 0.1, 2);
    } else {
      ObjRender_SetAlpha(objBoss, 200);
      ObjRender_SetBlendType(objBoss, BLEND_ADD_ARGB);
      MDrawJunkoLaughContinue(mainTimer);
    }
  }
     

  if (mainTimer == 60) {TJunkoChargeUp(200, 80, [255, 80, 255])};
  UpdateStarHitbox;

  mainTimer++;
  yield;
}

task ang_circle(posX, posY, offset, inc, proj, accel, max, ang_v) {
  
  let angle = offset;
  let angle_inc = inc;

  loop(360/angle_inc) {
    angle+=angle_inc;
    let shot = CreateShotA2(posX, posY, 0, angle, accel, max, proj, 5);
    ObjMove_SetAngularVelocity(shot, ang_v);
    ObjShot_SetSpellResist(shot, true);
  }

}

function circle_in(posX, posY, radius, offset, inc, proj, start, accel, max, delay) {
  
  let angle = offset;
  let angle_inc = inc;
  let shots = [];

  loop(360/angle_inc) {
    angle+=angle_inc;
    let shot = CreateShotA2(posX + cos(angle)*radius, posY + sin(angle)*radius, start, 180+angle, accel, max, proj, delay);
    ObjShot_SetAutoDelete(shot, false);
    shots = shots ~ [shot];
  }
  return shots;

}

task MainTask {

  let shoot_sfx = SoundSFX(GetCurrentScriptDirectory() ~ "./sfx/se_kira00.wav");
  ObjSound_SetVolumeRate(shoot_sfx, 60);
  let laser_sfx = SoundSFX(GetCurrentScriptDirectory() ~ "./sfx/se_lazer00.wav");
  let boom_spawn = SoundSFX(GetCurrentScriptDirectory() ~ "./sfx/se_tan00.wav");
  let warning = SoundSFX(GetCurrentScriptDirectory() ~ "./sfx/se_boon01.wav");
  let warning2 = SoundSFX(GetCurrentScriptDirectory() ~ "./sfx/se_ch00.wav");
  let desperation = SoundSFX(GetCurrentScriptDirectory() ~ "./sfx/se_nep00.wav");
  TLoadColourShader("Purple");

  wait(200);
  PlaySFX(boom_spawn);
  TStarShockwave("purple");
  let loop_iters = 0;
  
  // laser variables
  let lasers = [];
  let laser_interval = 120;
  let laser_count = 12;
  let angleL = 0;

  // blender variables
  let blender_offset = GetAngleToPlayer(objBoss);
  let blender_speed = 0.5 * (-1)^rand_int(0,9);
  let blender_target_speed = blender_speed;
  let blender_count = 7;
  let blender_wave = 3;
  let star_spin = blender_speed;

  // timer phase breakpoints - in seconds
  let blender_changes = [2*60, 7*60, 12*60, 16*60, 20*60, 23*60, 26*60, 28*60, 30*60];
  let blender_index = 0;
  let blender_shift_time = 30;

  // other varaibles
  let shotsToDelete = [];

  while(ObjEnemy_GetInfo(objBoss,INFO_LIFE) > 0){

    let starX = ObjMove_GetX(objStar);
    let starY = ObjMove_GetY(objStar);
    
    // laser
    if (loop_iters > blender_changes[0]) {
      if (loop_iters % laser_interval == 0) {
        lasers = [];
        angleL = (360/laser_count)/2;
        loop(laser_count) {
          let laser = CreateStraightLaserA1(starX, starY, angleL, 
                  512, 20, 20, 23, laser_interval);
          angleL += 360/laser_count;
          ObjLaser_SetGrazeInvalidFrame(laser, 5); 
          ObjLaser_SetInvalidLength(laser, 5, 5); 

          lasers = lasers ~ [laser];
        }
        PlaySFX(laser_sfx);
        PlaySFX(boom_spawn);
        TStarShockwave("purple");
      }

      let len = length(lasers);
      let magnitude = (laser_interval - (loop_iters % laser_interval))/laser_interval;

      ascent(i in 0..len) {
        let laser = lasers[i];
        let angle = ObjStLaser_GetAngle(laser);
        let direction = power(-1, i%2+1);
        ObjStLaser_SetAngle(laser, angle+magnitude*direction);
        ObjMove_SetPosition(laser, starX, starY);
        ObjLaser_SetLength(laser, ObjLaser_GetLength(laser))
      }

      if (loop_iters % laser_interval < laser_interval/5 && loop_iters > blender_changes[0] + laser_interval) {
        TColour(true);
      } else {
        TColour(false);
      }
    }
    

    // basic blender pattern
    ang_circle(starX, starY, blender_offset, 360/blender_count, 272, 0.1, blender_wave, 0);
    blender_offset+=blender_speed;

    // escalations

    if (blender_index > 1 && loop_iters % 180 == 0 && blender_index < length(blender_changes)) {
      let shots = circle_in(starX, starY, 260, rand(0, 360), 18, 204, 0, 0.05, 1.5, 30);
      ascent(i in 0..length(shots)) {
        let shot = shots[i];
        ObjShot_SetDeleteFrame(shot, 185);
      }
      shotsToDelete = shotsToDelete ~ shots;
      PlaySFX(shoot_sfx);
    }

    if (blender_index > 3 && loop_iters % 180 == 90 && blender_index < length(blender_changes)) {
      let shots = circle_in(starX, starY, 260, rand(0, 360), 18, 204, 0, 0.05, 1.5, 30);
      ascent(i in 0..length(shots)) {
        let shot = shots[i];
        ObjShot_SetDeleteFrame(shot, 185);
      }
      shotsToDelete = shotsToDelete ~ shots;
      PlaySFX(shoot_sfx);
    }
    // blender shifting

    // first block - bounds checking of phase change array, swap to movement if out of phase changes
    // (aka run into end of array)
    if (blender_index < length(blender_changes)) {
      let blender_phase = blender_changes[blender_index];

      if (loop_iters == blender_phase) {
        blender_index++;
        blender_target_speed = blender_speed*-1.07;
        PlaySFX(warning);
        if (blender_index == length(blender_changes) - 1) {
          ShockwaveCharge(starX, starY, 120, 6, [255, 80, 255]);
          PlaySFX(warning2);
        }
      }

      if (absolute(blender_speed) < absolute(blender_target_speed)) {
        let star_spin=blender_speed;
        SetStarSpinRate(star_spin);
        blender_speed+=blender_target_speed/blender_shift_time;
      }
    } else {
      if (blender_index == length(blender_changes)) {
        TStarShockwave("purple");
        PlaySFX(warning);
        PlaySFX(warning);
        PlaySFX(warning);
        PlaySFX(desperation);
        ascent(i in 0..length(shotsToDelete)) {
          ObjShot_FadeDelete(shotsToDelete[i]);
        }
        ScreenShakeA1(30, 3);
      } else {
        ScreenShakeA1(15, 2);
        TColour(true);
        SetStarMovement(1, GetAngleToPlayer(objStar));
        if (blender_index % 120 == 0) {
          PlaySFX(desperation);
        }
        if (blender_index % 9 == 0) {
          ang_circle(starX, starY, GetAngleToPlayer(objStar) + rand(-7, 7), 45, 204, 0.02, 4, 0);
          PlaySFX(shoot_sfx);
        }
      }
      blender_index++;
    }

    loop_iters++;
    yield;
  }
  ObjSound_Stop(desperation);
  TStarShockwave("purple");
  TColour(false);
  TUnloadColourShader;
}